/-
Copyright (c) 2025 Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.CategoryTheory.Monoidal.Grp_
import Toric.Mathlib.CategoryTheory.Monoidal.Grp_
import Mathlib.CategoryTheory.Monoidal.Yoneda
import Mathlib.Combinatorics.Quiver.ReflQuiver
import Toric.GroupScheme.YonedaGrp
import Toric.Mathlib.CategoryTheory.Monoidal.CommMon_

open CategoryTheory Mon_Class MonoidalCategory ChosenFiniteProducts
section

variable {C : Type*} [Category C] [MonoidalCategory C] {M N : Mon_ C}

instance {M N : Mon_ C} (f : M ‚ü∂ N) : IsMon_Hom f.hom := ‚ü®f.2, f.3‚ü©

def Mon_.homMk {M N : C} [Mon_Class M] [Mon_Class N] (f : M ‚ü∂ N) [IsMon_Hom f] :
    Mon_.mk' M ‚ü∂ Mon_.mk' N := ‚ü®f, IsMon_Hom.one_hom, IsMon_Hom.mul_hom‚ü©

end
section

attribute [local instance] monoidOfMon_Class

variable {C : Type*} [Category C] [ChosenFiniteProducts C] {M N : Mon_ C}  [IsCommMon N.X]

@[reassoc]
lemma Mon_Class.comp_mul {M N O : C} (f g : M ‚ü∂ N) (h : O ‚ü∂ M) [Mon_Class N] :
    h ‚â´ (f * g) = h ‚â´ f * h ‚â´ g :=
  (((yonedaMon.obj (.mk' N)).map (h.op)).hom.map_mul f g)

lemma Mon_Class.one_eq_one {M : C} [Mon_Class M] :
    Œ∑ = (1 : _ ‚ü∂ M) := by
  show _ = _ ‚â´ _
  rw [toUnit_unique (toUnit _) (ùüô _), Category.id_comp]

lemma Mon_Class.mul_eq_mul {M : C} [Mon_Class M] :
    Œº = fst M M * snd _ _ := by
  show _ = _ ‚â´ _
  rw [lift_fst_snd, Category.id_comp]

lemma Mon_.one_eq_one {M : Mon_ C} :
    M.one = 1 :=
  Mon_Class.one_eq_one (M := M.X)

lemma Mon_.mul_eq_mul {M : Mon_ C} :
    M.mul = (fst _ _ * snd _ _) :=
  Mon_Class.mul_eq_mul (M := M.X)

@[reassoc]
lemma Mon_Class.mul_comp {M N O : C} (f g : M ‚ü∂ N) (h : N ‚ü∂ O) [Mon_Class N] [Mon_Class O]
    [IsMon_Hom h] :
    (f * g) ‚â´ h = f ‚â´ h * g ‚â´ h := ((yonedaMon.map (Mon_.homMk h)).app (.op M)).hom.map_mul f g

@[reassoc (attr := simp)]
lemma Mon_Class.one_comp {M N O : C} (h : N ‚ü∂ O) [Mon_Class N] [Mon_Class O]
    [IsMon_Hom h] : (1 : M ‚ü∂ N) ‚â´ h = 1 := ((yonedaMon.map (Mon_.homMk h)).app (.op M)).hom.map_one

@[reassoc (attr := simp)]
lemma Mon_Class.comp_one {M N O : C} (h : M ‚ü∂ N) [Mon_Class O] :
    h ‚â´ (1 : N ‚ü∂ O) = 1 :=  ((yonedaMon.obj (.mk' O)).map (h.op)).hom.map_one

instance Hom.instCommMonoid {M N : C} [Mon_Class N] [IsCommMon N] : CommMonoid (M ‚ü∂ N) where
  mul_comm f g := by
    show lift _ _ ‚â´ _ = lift _ _ ‚â´ _
    conv_lhs => rw [‚Üê IsCommMon.mul_comm N]
    rw [‚Üê Category.assoc]
    congr 1
    ext <;> simp

@[reassoc]
lemma Mon_Class.comp_pow {M N O : C} (f : M ‚ü∂ N) (n : ‚Ñï) (h : O ‚ü∂ M) [Mon_Class N] :
    h ‚â´ f ^ n = (h ‚â´ f) ^ n := by
  induction' n with n hn
  ¬∑ simp
  simp only [pow_succ, Mon_Class.comp_mul, hn]

end

namespace Mon_.Hom

variable {C : Type*} [Category C] [ChosenFiniteProducts C] {M N : Mon_ C}

attribute [local instance] monoidOfMon_Class

instance instOne : One (M ‚ü∂ N) where
  one := {
    hom := 1
    one_hom := by simp [Mon_.one_eq_one]
    mul_hom := by simp [Mon_.mul_eq_mul, Mon_Class.comp_mul]
  }

lemma hom_one : (1 : (M ‚ü∂ N)).hom = 1 := rfl

variable [IsCommMon N.X]

instance instMul : Mul (M ‚ü∂ N) where
  mul f g :=
  { hom := f.hom * g.hom
    one_hom := by simp [Mon_.one_eq_one, Mon_Class.comp_mul, Mon_Class.one_comp]
    mul_hom := by
      simp [mul_eq_mul, comp_mul, mul_comp, mul_mul_mul_comm] }

@[simp]
lemma hom_mul (f g : M ‚ü∂ N) : (f * g).hom = f.hom * g.hom := rfl

instance instPow : Pow (M ‚ü∂ N) ‚Ñï where
  pow f n :=
  { hom := f.hom ^ n
    one_hom := by simp [Mon_.one_eq_one, Mon_Class.one_comp, Mon_Class.comp_pow]
    mul_hom := by
      simp [Mon_.mul_eq_mul, Mon_Class.comp_mul, Mon_Class.mul_comp, Mon_Class.comp_pow, mul_pow]
  }

@[simp]
lemma hom_pow (f : M ‚ü∂ N) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := rfl

instance : CommMonoid (M ‚ü∂ N) :=
  Function.Injective.commMonoid hom (fun _ _ ‚Ü¶ ext) hom_one hom_mul hom_pow

end  Mon_.Hom

section

open ChosenFiniteProducts MonoidalCategory

variable {C : Type*} [Category C] [ChosenFiniteProducts C] {G H : Grp_ C}

attribute [local instance] groupOfGrp_Class

def Grp_.homMk {G H : C} [Grp_Class G] [Grp_Class H] (f : G ‚ü∂ H) [IsMon_Hom f] :
    Grp_.mk' G ‚ü∂ Grp_.mk' H := ‚ü®f, IsMon_Hom.one_hom, IsMon_Hom.mul_hom‚ü©

@[reassoc]
lemma Grp_Class.comp_inv {G H K : C} (f : G ‚ü∂ H) (h : K ‚ü∂ G) [Grp_Class H] :
    h ‚â´ f‚Åª¬π = (h ‚â´ f)‚Åª¬π := ((yonedaGrp.obj (.mk' H)).map (h.op)).hom.map_inv f

@[reassoc]
lemma Grp_Class.comp_div {G H K : C} (f g : G ‚ü∂ H) (h : K ‚ü∂ G) [Grp_Class H] :
    h ‚â´ (f / g) = h ‚â´ f / h ‚â´ g :=
  (((yonedaGrp.obj (.mk' H)).map (h.op)).hom.map_div f g)

@[reassoc]
lemma Grp_Class.div_comp {G H K : C} (f g : G ‚ü∂ H) (h : H ‚ü∂ K) [Grp_Class H] [Grp_Class K]
    [IsMon_Hom h] : (f / g) ‚â´ h =  (f ‚â´ h) / (g ‚â´ h) :=
    (((yonedaGrp.map (Grp_.homMk h)).app (.op G)).hom.map_div f g)

lemma Grp_Class.inv_eq_comp_inv {G H : C} (f : G ‚ü∂ H) [Grp_Class H] : f ‚â´ Œπ = f‚Åª¬π := rfl

lemma Grp_Class.mul_eq_comp_mul {G H : C} {f g : G ‚ü∂ H} [Grp_Class H] : f * g = lift f g ‚â´ Œº := rfl

lemma Grp_Class.mul_inv_rev {G : C} [Grp_Class G] :
    Œº ‚â´ Œπ = ((Œπ : G ‚ü∂ G) ‚äó Œπ) ‚â´ (Œ≤_ _ _).hom ‚â´ Œº := by
  calc
    _ = ((fst G G) * (snd G G)) ‚â´ Œπ := by rw [mul_eq_mul]
    _ = (snd G G)‚Åª¬π * (fst G G)‚Åª¬π := by rw [Grp_Class.inv_eq_comp_inv]; simp
    _ = lift (snd G G ‚â´ Œπ) (fst G G ‚â´ Œπ) ‚â´ Œº := rfl
    _ = lift (fst G G ‚â´ Œπ) (snd G G ‚â´ Œπ) ‚â´ (Œ≤_ G G).hom ‚â´ Œº := by simp
    _ = ((Œπ : G ‚ü∂ G) ‚äó Œπ) ‚â´ (Œ≤_ _ _).hom ‚â´ Œº := by simp

instance Hom.instCommGroup {G H : C} [Grp_Class H] [IsCommMon H] :
    CommGroup (G ‚ü∂ H) where
  __ := Hom.instCommMonoid
  inv_mul_cancel f := by simp

@[reassoc]
lemma Grp_Class.comp_zpow {G H K : C} [Grp_Class H] (f : G ‚ü∂ H) (h : K ‚ü∂ G) :
    ‚àÄ n : ‚Ñ§, h ‚â´ f ^ n = (h ‚â´ f) ^ n
  | (n : ‚Ñï) => by simp [comp_pow]
  | .negSucc n => by simp [comp_pow, comp_inv]

namespace Grp_.Hom

instance instOne : One (G ‚ü∂ H) := inferInstanceAs <| One (G.toMon_ ‚ü∂ H.toMon_)

lemma hom_one : (1 : (G ‚ü∂ H)).hom = 1 := rfl

variable [IsCommMon H.X]

instance instMul : Mul (G ‚ü∂ H) := inferInstanceAs <| Mul (G.toMon_ ‚ü∂ H.toMon_)

@[simp]
lemma hom_mul (f g : G ‚ü∂ H) : (f * g).hom = f.hom * g.hom := rfl

instance instPow : Pow (G ‚ü∂ H) ‚Ñï := inferInstanceAs <| Pow (G.toMon_ ‚ü∂ H.toMon_) ‚Ñï

@[simp]
lemma hom_pow (f : G ‚ü∂ H) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := rfl

instance {G : C} [Grp_Class G] [IsCommMon G] : IsMon_Hom (Œπ : G ‚ü∂ G) where
  one_hom := by simp only [one_eq_one]; exact inv_one
  mul_hom := by
    simp [Grp_Class.mul_inv_rev]

instance {f : G ‚ü∂ H} [IsCommMon H.X] : IsMon_Hom f.hom‚Åª¬π where
  one_hom := by
    change _ ‚â´ _ ‚â´ _ = _
    simp [Mon_Class.one_eq_one, one_comp]
  mul_hom := by
    simp [‚Üê Grp_Class.inv_eq_comp_inv]

instance instInv : Inv (G ‚ü∂ H) where
  inv f := {
    hom := f.hom‚Åª¬π
    one_hom := by simp only [Mon_.one_eq_one, one_comp_assoc]; rw [one_comp]
    mul_hom := by simp [Mon_.mul_eq_mul, Mon_Class.comp_mul, Mon_Class.mul_comp]
  }

@[simp]
lemma hom_inv (f : G ‚ü∂ H) : (f‚Åª¬π).hom = f.hom‚Åª¬π := rfl

instance instDiv : Div (G ‚ü∂ H) where
  div f g :=
  { hom := f.hom / g.hom
    one_hom := by simp [Mon_.one_eq_one, Grp_Class.comp_div, Mon_Class.one_comp]
    mul_hom := by
      simp [Mon_.mul_eq_mul, Grp_Class.comp_div, Mon_Class.comp_mul, Mon_Class.mul_comp,
          mul_div_mul_comm] }

@[simp]
lemma hom_div (f g : G ‚ü∂ H) : (f / g).hom = f.hom / g.hom := rfl

instance instPowInt : Pow (G ‚ü∂ H) ‚Ñ§ where
  pow f i := {
    hom := f.hom ^ i
    one_hom := by simp [Mon_.one_eq_one, Mon_Class.one_comp, Grp_Class.comp_zpow]
    mul_hom := by
      simp [Mon_.mul_eq_mul, Mon_Class.comp_mul, Mon_Class.mul_comp, Grp_Class.comp_zpow, mul_zpow]
  }

@[simp]
lemma hom_zpow (f : G ‚ü∂ H) (n : ‚Ñ§) : (f ^ n).hom = f.hom ^ n := rfl

instance instCommGroup : CommGroup (G ‚ü∂ H) :=
  Function.Injective.commGroup Mon_.Hom.hom (fun _ _ ‚Ü¶ Mon_.Hom.ext)
    hom_one hom_mul hom_inv hom_div hom_pow hom_zpow

end Grp_.Hom
end
